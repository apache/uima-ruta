// Licensed to the Apache Software Foundation (ASF) under one
// or more contributor license agreements. See the NOTICE file
// distributed with this work for additional information
// regarding copyright ownership. The ASF licenses this file
// to you under the Apache License, Version 2.0 (the
// "License"); you may not use this file except in compliance
// with the License. You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing,
// software distributed under the License is distributed on an
// "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied. See the License for the
// specific language governing permissions and limitations
// under the License.

[[_ugr.tools.ruta.language.conditions]]
= Conditions

[[_ugr.tools.ruta.language.conditions.after]]
== AFTER

The AFTER condition evaluates true, if the matched annotation starts after the beginning of an arbitrary annotation of the passed type.
If a list of types is passed, this has to be true for at least one of them. 

.Definition
[source]
----
AFTER(Type|TypeListExpression)
----

.Example
[source]
----
CW{AFTER(SW)};
----

Here, the rule matches on a capitalized word, if there is any small written word previously. 

[[_ugr.tools.ruta.language.conditions.and]]
== AND

The AND condition is a composed condition and evaluates true, if all contained conditions evaluate true. 

.Definition
[source]
----
AND(Condition1,...,ConditionN)
----

.Example
[source]
----
Paragraph{AND(PARTOF(Headline),CONTAINS(Keyword))
          ->MARK(ImportantHeadline)};
----

In this example, a paragraph is annotated with an ImportantHeadline annotation, if it is part of a Headline and contains a Keyword annotation. 

[[_ugr.tools.ruta.language.conditions.before]]
== BEFORE

The BEFORE condition evaluates true, if the matched annotation starts before the beginning of an arbitrary annotation of the passed type.
If a list of types is passed, this has to be true for at least one of them. 

.Definition
[source]
----
BEFORE(Type|TypeListExpression)
----

.Example
[source]
----
CW{BEFORE(SW)};
----

Here, the rule matches on a capitalized word, if there is any small written word afterwards. 

[[_ugr.tools.ruta.language.conditions.contains]]
== CONTAINS

The CONTAINS condition evaluates true on a matched annotation, if the frequency of the passed type lies within an optionally passed interval.
The limits of the passed interval are per default interpreted as absolute numeral values.
By passing a further boolean parameter set to true the limits are interpreted as percental values.
If no interval parameters are passed at all, then the condition checks whether the matched annotation contains at least one occurrence of the passed type. 

.Definition
[source]
----
CONTAINS(Type(,NumberExpression,NumberExpression(,BooleanExpression)?)?)
----

.Example
[source]
----
Paragraph{CONTAINS(Keyword)->MARK(KeywordParagraph)};
----

A Paragraph is annotated with a KeywordParagraph annotation, if it contains a Keyword annotation. 


[source]
----
Paragraph{CONTAINS(Keyword,2,4)->MARK(KeywordParagraph)};
----

A Paragraph is annotated with a KeywordParagraph annotation, if it contains between two and four Keyword annotations. 


[source]
----
Paragraph{CONTAINS(Keyword,50,100,true)->MARK(KeywordParagraph)};
----

A Paragraph is annotated with a KeywordParagraph annotation, if it contains between 50% and 100% Keyword annotations.
This is calculated based on the tokens of the Paragraph.
If the Paragraph contains six basic annotations (see <<_ugr.tools.ruta.language.seeding>>), two of them are part of one Keyword annotation, and if one basic annotation is also annotated with a Keyword annotation, then the percentage of the contained Keywords is 50%. 

[[_ugr.tools.ruta.language.conditions.contextcount]]
== CONTEXTCOUNT

The CONTEXTCOUNT condition numbers all occurrences of the matched type within the context of a passed type's annotation consecutively, thus assigning an index to each occurrence.
Additionally it stores the index of the matched annotation in a numerical variable if one is passed.
The condition evaluates true if the index of the matched annotation is within a passed interval.
If no interval is passed, the condition always evaluates true. 

.Definition
[source]
----
CONTEXTCOUNT(Type(,NumberExpression,NumberExpression)?(,Variable)?)
----

.Example
[source]
----
Keyword{CONTEXTCOUNT(Paragraph,2,3,var)
          ->MARK(SecondOrThirdKeywordInParagraph)};
----

Here, the position of the matched Keyword annotation within a Paragraph annotation is calculated and stored in the variable 'var'. If the counted value lies within the interval [2,3], then the matched Keyword is annotated with the SecondOrThirdKeywordInParagraph annotation. 

[[_ugr.tools.ruta.language.conditions.count]]
== COUNT

The COUNT condition can be used in two different ways.
In the first case (see first definition), it counts the number of annotations of the passed type within the window of the matched annotation and stores the amount in a numerical variable, if such a variable is passed.
The condition evaluates true if the counted amount is within a specified interval.
If no interval is passed, the condition always evaluates true.
In the second case (see second definition), it counts the number of occurrences of the passed VariableExpression (second parameter) within the passed list (first parameter) and stores the amount in a numerical variable, if such a variable is passed.
Again, the condition evaluates true if the counted amount is within a specified interval.
If no interval is passed, the condition always evaluates true. 

.Definition
[source]
----
COUNT(Type(,NumberExpression,NumberExpression)?(,NumberVariable)?)
----


[source]
----
COUNT(ListExpression,VariableExpression
          (,NumberExpression,NumberExpression)?(,NumberVariable)?)
----

.Example
[source]
----
Paragraph{COUNT(Keyword,1,10,var)->MARK(KeywordParagraph)};
----

Here, the amount of Keyword annotations within a Paragraph is calculated and stored in the variable 'var'. If one to ten Keywords were counted, the paragraph is marked with a KeywordParagraph annotation. 


[source]
----
Paragraph{COUNT(list,"author",5,7,var)};
----

Here, the number of occurrences of STRING "author" within the STRINGLIST 'list' is counted and stored in the variable 'var'. If "author" occurs five to seven times within 'list', the condition evaluates true. 

[[_ugr.tools.ruta.language.conditions.currentcount]]
== CURRENTCOUNT

The CURRENTCOUNT condition numbers all occurrences of the matched type within the whole document consecutively, thus assigning an index to each occurrence.
Additionally, it stores the index of the matched annotation in a numerical variable, if one is passed.
The condition evaluates true if the index of the matched annotation is within a specified interval.
If no interval is passed, the condition always evaluates true. 

.Definition
[source]
----
CURRENTCOUNT(Type(,NumberExpression,NumberExpression)?(,Variable)?)
----

.Example
[source]
----
Paragraph{CURRENTCOUNT(Keyword,3,3,var)->MARK(ParagraphWithThirdKeyword)};
----

Here, the Paragraph, which contains the third Keyword of the whole document, is annotated with the ParagraphWithThirdKeyword annotation.
The index is stored in the variable 'var'. 

[[_ugr.tools.ruta.language.conditions.endswith]]
== ENDSWITH

The ENDSWITH condition evaluates true, if an annotation of the given type ends exactly at the same position as the matched annotation.
If a list of types is passed, this has to be true for at least one of them. 

.Definition
[source]
----
ENDSWITH(Type|TypeListExpression)
----

.Example
[source]
----
Paragraph{ENDSWITH(SW)};
----

Here, the rule matches on a Paragraph annotation, if it ends with a small written word. 

[[_ugr.tools.ruta.language.conditions.feature]]
== FEATURE

The FEATURE condition compares a feature of the matched annotation with the second argument. 

.Definition
[source]
----
FEATURE(StringExpression,Expression)
----

.Example
[source]
----
Document{FEATURE("language",targetLanguage)}
----

This rule matches, if the feature named 'language' of the document annotation equals the value of the variable 'targetLanguage'. 

[[_ugr.tools.ruta.language.conditions.if]]
== IF

The IF condition evaluates true, if the contained boolean expression evaluates true. 

.Definition
[source]
----
IF(BooleanExpression)
----

.Example
[source]
----
Paragraph{IF(keywordAmount > 5)->MARK(KeywordParagraph)};
----

A Paragraph annotation is annotated with a KeywordParagraph annotation, if the value of the variable 'keywordAmount' is greater than five. 

[[_ugr.tools.ruta.language.conditions.inlist]]
== INLIST

The INLIST condition is fulfilled, if the matched annotation is listed in a given word or string list.
If an optional agrument is given, then  the value of the argument is used instead of the covered text of the matched annotation 

.Definition
[source]
----
INLIST(WordList(,StringExpression)?)
----


[source]
----
INLIST(StringList(,StringExpression)?)
----

.Example
[source]
----
Keyword{INLIST(SpecialKeywordList)->MARK(SpecialKeyword)};
----

A Keyword is annotated with the type SpecialKeyword, if the text of the Keyword annotation is listed in the word list or string list SpecialKeywordList. 


[source]
----
Token{INLIST(MyLemmaList, Token.lemma)->MARK(SpecialLemma)};
----

This rule creates an annotation of the type SpecialLemma for each token that provides a feature value  of the feature "lemma" that is present in the string list or word list MyLemmaList. 

[[_ugr.tools.ruta.language.conditions.is]]
== IS

The IS condition evaluates true, if there is an annotation of the given type with the same beginning and ending offsets as the matched annotation.
If a list of types is given, the condition evaluates true, if at least one of them fulfills the former condition. 

.Definition
[source]
----
IS(Type|TypeListExpression)
----

.Example
[source]
----
Author{IS(Englishman)->MARK(EnglishAuthor)};
----

If an Author annotation is also annotated with an Englishman annotation, it is annotated with an EnglishAuthor annotation. 

[[_ugr.tools.ruta.language.conditions.last]]
== LAST

The LAST condition evaluates true, if the type of the last token within the window of the matched annotation is of the given type. 

.Definition
[source]
----
LAST(TypeExpression)
----

.Example
[source]
----
Document{LAST(CW)};
----

This rule fires, if the last token of the document is a capitalized word. 

[[_ugr.tools.ruta.language.conditions.mofn]]
== MOFN

The MOFN condition is a composed condition.
It evaluates true if the number of containing conditions evaluating true is within a given interval. 

.Definition
[source]
----
MOFN(NumberExpression,NumberExpression,Condition1,...,ConditionN)
----

.Example
[source]
----
Paragraph{MOFN(1,1,PARTOF(Headline),CONTAINS(Keyword))
          ->MARK(HeadlineXORKeywords)};
----

A Paragraph is marked as a HeadlineXORKeywords, if the matched text is either part of a Headline annotation or contains Keyword annotations. 

[[_ugr.tools.ruta.language.conditions.near]]
== NEAR

The NEAR condition is fulfilled, if the distance of the matched annotation to an annotation of the given type is within a given interval.
The direction is defined by a boolean parameter, whose default value is set to true, therefore searching forward.
By default this condition works on an unfiltered index.
An optional fifth boolean parameter can be set to true to get the condition being evaluated on a filtered index. 

.Definition
[source]
----
NEAR(TypeExpression,NumberExpression,NumberExpression
          (,BooleanExpression(,BooleanExpression)?)?)
----

.Example
[source]
----
Paragraph{NEAR(Headline,0,10,false)->MARK(NoHeadline)};
----

A Paragraph that starts at most ten tokens after a Headline annotation is annotated with the NoHeadline annotation. 

[[_ugr.tools.ruta.language.conditions.not]]
== NOT

The NOT condition negates the result of its contained condition. 

.Definition
[source]
----
"-"Condition
----

.Example
[source]
----
Paragraph{-PARTOF(Headline)->MARK(Headline)};
----

A Paragraph that is not part of a Headline annotation so far is annotated with a Headline annotation. 

[[_ugr.tools.ruta.language.conditions.or]]
== OR

The OR Condition is a composed condition and evaluates true, if at least one contained condition is evaluated true. 

.Definition
[source]
----
OR(Condition1,...,ConditionN)
----

.Example
[source]
----
Paragraph{OR(PARTOF(Headline),CONTAINS(Keyword))
                                           ->MARK(ImportantParagraph)};
----

In this example a Paragraph is annotated with the ImportantParagraph annotation, if it is a Headline or contains Keyword annotations. 

[[_ugr.tools.ruta.language.conditions.parse]]
== PARSE

The PARSE condition is fulfilled, if the text covered by the matched annotation or the text defined by a optional first argument can be transformed into a value of the given variable's type.
If this is possible, the parsed value is additionally assigned to the passed variable.
For numeric values, this conditions delegates to the NumberFormat of the locale given by the optional last argument.
Therefore, this condition parses the string "`2,3`" for the locale "`en`" to the value 23. 

.Definition
[source]
----
PARSE((stringExpression,)? variable(, stringExpression)?)
----

.Example
[source]
----
NUM{PARSE(var,"de")};
n:NUM{PARSE(n.ct,var,"de")};
----

If the variable 'var' is of an appropriate numeric type for the locale "de", the value of NUM is parsed and subsequently stored in 'var'. 

[[_ugr.tools.ruta.language.conditions.partof]]
== PARTOF

The PARTOF condition is fulfilled, if the matched annotation is part of an annotation of the given type.
However, it is not necessary that the matched annotation is smaller than the annotation of the given type.
Use the (much slower) PARTOFNEQ condition instead, if this is needed.
If a type list is given, the condition evaluates true, if the former described condition for a single type is fulfilled for at least one of the types in the list. 

.Definition
[source]
----
PARTOF(Type|TypeListExpression)
----

.Example
[source]
----
Paragraph{PARTOF(Headline) -> MARK(ImportantParagraph)};
----

A Paragraph is an ImportantParagraph, if the matched text is part of a Headline annotation. 

[[_ugr.tools.ruta.language.conditions.partofneq]]
== PARTOFNEQ

The PARTOFNEQ condition is fulfilled if the matched annotation is part of (smaller than and inside of) an annotation of the given type.
If also annotations of the same size should be acceptable, use the PARTOF condition.
If a type list is given, the condition evaluates true if the former described condition is fulfilled for at least one of the types in the list. 

.Definition
[source]
----
PARTOFNEQ(Type|TypeListExpression)
----

.Example
[source]
----
W{PARTOFNEQ(Headline) -> MARK(ImportantWord)};
----

A word is an "`ImportantWord`", if it is part of a headline. 

[[_ugr.tools.ruta.language.conditions.position]]
== POSITION

The POSITION condition is fulfilled, if the matched type is the k-th occurrence of this type within the window of an annotation of the passed type, whereby k is defined by the value of the passed NumberExpression.
If the additional boolean paramter is set to false, then k counts the occurrences of of the minimal annotations. 

.Definition
[source]
----
POSITION(Type,NumberExpression(,BooleanExpression)?)
----

.Example
[source]
----
Keyword{POSITION(Paragraph,2)->MARK(SecondKeyword)};
----

The second Keyword in a Paragraph is annotated with the type SecondKeyword. 


[source]
----
Keyword{POSITION(Paragraph,2,false)->MARK(SecondKeyword)};
----

A Keyword in a Paragraph is annotated with the type SecondKeyword, if it starts at the same offset as the second (visible) RutaBasic annotation, which normally corresponds to the tokens. 

[[_ugr.tools.ruta.language.conditions.regexp]]
== REGEXP

The REGEXP condition is fulfilled, if the given pattern matches on the matched annotation.
However, if a string variable is given as the first argument, then the pattern is evaluated on the value of the variable.
For more details on the syntax of regular expressions, take a look at the http://docs.oracle.com/javase/1.4.2/docs/api/java/util/regex/Pattern.html[Java API]. By default the REGEXP condition is case-sensitive.
To change this, add an optional boolean parameter, which is set to true.
The regular expression is  initialized with the flags DOTALL and MULTILINE, and if the optional parameter is set to true,  then additionally with the flags CASE_INSENSITIVE and UNICODE_CASE. 

.Definition
[source]
----
REGEXP((StringVariable,)? StringExpression(,BooleanExpression)?)
----

.Example
[source]
----
Keyword{REGEXP("..")->MARK(SmallKeyword)};
----

A Keyword that only consists of two chars is annotated with a SmallKeyword annotation. 

[[_ugr.tools.ruta.language.conditions.score]]
== SCORE

The SCORE condition evaluates the heuristic score of the matched annotation.
This score is set or changed by the MARK action.
The condition is fulfilled, if the score of the matched annotation is in a given interval.
Optionally, the score can be stored in a variable. 

.Definition
[source]
----
SCORE(NumberExpression,NumberExpression(,Variable)?)
----

.Example
[source]
----
MaybeHeadline{SCORE(40,100)->MARK(Headline)};
----

An annotation of the type MaybeHeadline is annotated with Headline, if its score is between 40 and 100. 

[[_ugr.tools.ruta.language.conditions.size]]
== SIZE

The SIZE contition counts the number of elements in the given list.
By default, this condition always evaluates true.
When an interval is passed, it evaluates true, if the counted number of list elements is within the interval.
The counted number can be stored in an optionally passed numeral variable. 

.Definition
[source]
----
SIZE(ListExpression(,NumberExpression,NumberExpression)?(,Variable)?)
----

.Example
[source]
----
Document{SIZE(list,4,10,var)};
----

This rule fires, if the given list contains between 4 and 10 elements.
Additionally, the exact amount is stored in the variable "`var`". 

[[_ugr.tools.ruta.language.conditions.startswith]]
== STARTSWITH

The STARTSWITH condition evaluates true, if an annotation of the given type starts exactly at the same position as the matched annotation.
If a type list is given, the condition evaluates true, if the former is true for at least one of the given types in the list. 

.Definition
[source]
----
STARTSWITH(Type|TypeListExpression)
----

.Example
[source]
----
Paragraph{STARTSWITH(SW)};
----

Here, the rule matches on a Paragraph annotation, if it starts with small written word. 

[[_ugr.tools.ruta.language.conditions.totalcount]]
== TOTALCOUNT

The TOTALCOUNT condition counts the annotations of the passed type within the whole document and stores the amount in an optionally passed numerical variable.
The condition evaluates true, if the amount is within the passed interval.
If no interval is passed, the condition always evaluates true. 

.Definition
[source]
----
TOTALCOUNT(Type(,NumberExpression,NumberExpression)?(,NumberVariable)?)
----

.Example
[source]
----
Paragraph{TOTALCOUNT(Keyword,1,10,var)->MARK(KeywordParagraph)};
----

Here, the amount of Keyword annotations within the whole document is calculated and stored in the variable 'var'. If one to ten Keywords were counted, the Paragraph is marked with a KeywordParagraph annotation. 

[[_ugr.tools.ruta.language.conditions.vote]]
== VOTE

The VOTE condition counts the annotations of the given two types within the window of the matched annotation and evaluates true, if it finds more annotations of the first type. 

.Definition
[source]
----
VOTE(TypeExpression,TypeExpression)
----

.Example
[source]
----
Paragraph{VOTE(FirstName,LastName)};
----

Here, this rule fires, if a paragraph contains more firstnames than lastnames. 